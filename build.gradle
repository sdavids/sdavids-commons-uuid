/*
 * Copyright (c) 2017-2018, Sebastian Davids
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static java.io.File.createTempFile

import com.github.spotbugs.SpotBugsTask
import de.thetaphi.forbiddenapis.gradle.CheckForbiddenApis
import net.ltgt.gradle.errorprone.CheckSeverity

// https://plugins.gradle.org/
plugins {
  id 'com.gradle.build-scan' version '1.16'
  id 'java-library'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'net.nemerosa.versioning' version '2.7.1'
  id 'com.diffplug.gradle.spotless' version '3.14.0'
  id 'net.ltgt.errorprone' version '0.6'
  id 'de.thetaphi.forbiddenapis' version '2.6'
  id 'com.github.spotbugs' version '1.6.3'
  id 'ru.vyarus.animalsniffer' version '1.4.6'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'maven'
}

ext {
  rootPackageName = 'io.sdavids.commons.uuid'

  // https://bintray.com/bintray/jcenter
  errorproneVersion = '2.3.1'
  errorproneJavacVersion = '9+181-r4173-1'
  checkstyleVersion = '8.12'
  pmdVersion = '6.7.0'
  spotbugsVersion = '3.1.7'
  findsecbugsVersion = '1.8.0'
  java8SignatureVersion = '1.0'
  jacocoVersion = '0.8.2'
  guavaVersion = '23.0'
  joolVersion = '0.9.12'
  junitVersion = '4.12'
  assertjVersion = '3.11.1'
  assertjGuavaVersion = '3.1.0'
  commonsTestVersion = '1.1.1'

  vendor = 'Sebastian Davids'

  javadocLinks = [
      'http://docs.oracle.com/javase/8/docs/api/'
  ] as String[]
}

group 'io.sdavids.commons'
version = '2.1.0'

repositories {
  jcenter()
}

dependencies {
  signature "org.codehaus.mojo.signature:java18:${java8SignatureVersion}@signature"
  errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
  errorproneJavac "com.google.errorprone:javac:${errorproneJavacVersion}"
  checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
  pmd "net.sourceforge.pmd:pmd-core:${pmdVersion}"
  pmd "net.sourceforge.pmd:pmd-java:${pmdVersion}"
  spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:${findsecbugsVersion}"

  implementation("com.google.guava:guava:${guavaVersion}") {
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
    exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
  }

  testImplementation("io.sdavids.commons.test:sdavids-commons-test:${commonsTestVersion}")
  testImplementation("org.jooq:jool:${joolVersion}")
  testImplementation("junit:junit:${junitVersion}")
  testImplementation("org.assertj:assertj-core:${assertjVersion}")
  testImplementation("org.assertj:assertj-guava:${assertjGuavaVersion}") {
    exclude group: 'com.google.guava', module: 'guava'
  }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile).configureEach {
  options.fork = true
  options.encoding = 'UTF-8'
  options.debugOptions.debugLevel = 'source,lines,vars'
  options.deprecation = true
  // https://docs.oracle.com/javase/10/tools/javac.htm#GUID-AEEC9F07-CB49-4E96-8BC7-BCC2C7F725C9__ENABLEORDISABLEWARNINGSWITHTHE-XLIN-7D3DB0EE
  options.compilerArgs += ['-parameters', '-Xlint:all,-processing,-path', '-Werror']
  options.errorprone {
    disableWarningsInGeneratedCode = true
    allDisabledChecksAsWarnings = true
    check('StaticOrDefaultInterfaceMethod', CheckSeverity.OFF)
    check('ReturnMissingNullable', CheckSeverity.OFF)
    check('Var', CheckSeverity.OFF)
  }
}

tasks.named('jar').configure {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
  dirMode = 0775
  fileMode = 0664
  manifest {
    attributes(
        'Build-Revision': versioning.info.commit,
        'Specification-Title': project.name,
        'Specification-Version': project.version,
        'Specification-Vendor': project.vendor,
        'Implementation-Title': project.name,
        'Implementation-Version': "${project.version}-${versioning.info.build}",
        'Implementation-Vendor': project.vendor,
        'Automatic-Module-Name': project.rootPackageName
    )
  }
  from("${project.rootDir}") {
    include 'NOTICE'
    include 'LICENSE'
    into 'META-INF'
  }
  exclude('rebel.xml')
}

tasks.withType(CheckForbiddenApis).configureEach {
  // https://github.com/policeman-tools/forbidden-apis/wiki/BundledSignatures
  bundledSignatures = [
      'jdk-internal',
      'jdk-unsafe',
      'jdk-deprecated',
      'jdk-non-portable',
      'jdk-system-out',
      'jdk-reflection'
  ]
}

tasks.withType(Checkstyle).configureEach {
  configFile = project.file('gradle/conf/checkstyle.xml')
  configDir = project.provider({ project.file('gradle/conf/') })
  maxWarnings = 0
  reports {
    xml.enabled false
    html.enabled project.hasProperty('createReports')
  }
}

tasks.withType(Pmd).configureEach {
  ruleSets = []
  ruleSetFiles = project.files('gradle/conf/pmd.xml')
  reports {
    xml.enabled false
    html.enabled project.hasProperty('createReports')
  }
}

spotbugs {
  toolVersion = "${spotbugsVersion}"
  excludeFilter = project.file('gradle/conf/spotbugs-exclude-filter.xml')
}

tasks.withType(SpotBugsTask).configureEach {
  effort = 'max'
  reports {
    xml.enabled false
    html.enabled project.hasProperty('createReports')
  }
}

jacoco {
  toolVersion = "${jacocoVersion}"
}

jacocoTestReport {
  reports {
    xml.enabled false
    html.enabled project.hasProperty('createReports')
  }
}

if (project.hasProperty('createReports')) {
  check.dependsOn jacocoTestReport
}

spotless {
  format 'misc', {
    target '.gitattributes', '.gitignore',
        'README.adoc',
        'release.sh',
        'CODING_STYLE.txt',
        'AUTHORS', 'CONTRIBUTORS', 'LICENSE', 'NOTICE'
    trimTrailingWhitespace()
    endWithNewline()
  }
  format 'properties', {
    target 'gradle.properties'
    encoding 'ISO-8859-1'
    trimTrailingWhitespace()
    endWithNewline()
  }
  format 'yaml', {
    target '.travis.yml'
    indentWithSpaces 2
    trimTrailingWhitespace()
    endWithNewline()
  }
  format 'xml', {
    target 'gradle/conf/**/*.xml'
    indentWithSpaces 2
    trimTrailingWhitespace()
    endWithNewline()
  }
  java {
    googleJavaFormat '1.6'
    licenseHeaderFile "${project.projectDir}/gradle/conf/spotless-license-java.txt"
  }
  groovyGradle {
    target 'build.gradle', 'settings.gradle'
  }
}

test {
  systemProperty 'user.timezone', System.properties['user.timezone']
  systemProperty 'user.language', System.properties['user.language']
  systemProperty 'user.country', System.properties['user.country']

  // injectable singleton needs a new VM for each test case
  forkEvery 1

  reports.junitXml.enabled = false
  reports.html.enabled = project.hasProperty('createReports')

  testLogging {
    exceptionFormat = 'full'
  }
}

buildScan {
  termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'
}

wrapper {
  gradleVersion = '4.10'
}

if (project.hasProperty('release')) {

  apply plugin: 'signing'
  apply plugin: 'maven-publish'

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
  }

  task sourcesJar(type: Jar, dependsOn: compileJava) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  javadoc {
    options.encoding = 'UTF-8'
    options.memberLevel = 'PROTECTED'
    options.header = "${project.name} ${project.version}"
    options.docTitle = "${project.name} API"
    options.links = javadocLinks
    options.addStringOption '-quiet'
  }

  artifacts {
    archives javadocJar, sourcesJar
  }

  signing {
    sign configurations.archives
  }

  final pomConfig = {
    name "${project.name}"
    licenses {
      license {
        name 'Apache License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }
    developers {
      developer {
        id 'sdavids'
        name 'Sebastian Davids'
        email 'sdavids@gmx.de'
        timezone 'Europe/Berlin'
      }
    }
    scm {
      url 'https://github.com/sdavids/sdavids-commons-uuid'
      connection 'scm:git:https://github.com/sdavids/sdavids-commons-uuid.git'
    }
    issueManagement {
      system 'GitHub'
      url 'https://github.com/sdavids/sdavids-commons-uuid/issues'
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourcesJar {
          classifier 'sources'
        }

        artifact javadocJar {
          classifier 'javadoc'
        }

        pom.withXml {
          asNode().children().last() + pomConfig

          // workaround
          // "Signing the generated POM file generated by this plugin is currently not supported."
          // https://docs.gradle.org/current/userguide/publishing_maven.html
          final tmp = createTempFile("generated-pom", ".xml")
          tmp.deleteOnExit()

          writeTo(tmp)

          final pomAscFile = signing.sign(tmp).signatureFiles[0]

          artifact(pomAscFile) {
            classifier = null
            extension = 'pom.asc'
          }

          tmp.delete()
          //
        }

        // workaround
        // asc files not uploaded
        project.tasks.signArchives.signatureFiles.each {
          artifact(it) {
            def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
            classifier = matcher.find() ? matcher.group(1) : null
            extension = 'jar.asc'
          }
        }
        //
      }
    }
  }

  bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
      repo = 'sdavids'
      name = 'sdavids-commons-uuid'
      licenses = ['Apache-2.0']
      websiteUrl = 'https://github.com/sdavids/sdavids-commons-uuid'
      issueTrackerUrl = 'https://github.com/sdavids/sdavids-commons-uuid/issues'
      vcsUrl = 'https://github.com/sdavids/sdavids-commons-uuid.git'
      githubRepo = 'sdavids/sdavids-commons-uuid'
      publicDownloadNumbers = true
      version {
        name = "${project.version}"
        vcsTag = "v${project.version}"
      }
    }
  }
}
